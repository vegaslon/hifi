apply plugin: 'com.android.application'

ext.RELEASE_NUMBER = project.hasProperty('RELEASE_NUMBER') ? project.getProperty('RELEASE_NUMBER') : '0'
ext.RELEASE_TYPE = project.hasProperty('RELEASE_TYPE') ? project.getProperty('RELEASE_TYPE') : 'DEV'
ext.BUILD_BRANCH = project.hasProperty('BUILD_BRANCH') ? project.getProperty('BUILD_BRANCH') : ''

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "org.saintandreas.testapp"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        ndk { abiFilters 'arm64-v8a' }
        externalNativeBuild {
            cmake {
                arguments '-DHIFI_ANDROID=1',
                    '-DANDROID_PLATFORM=android-24',
                    '-DANDROID_TOOLCHAIN=clang',
                    '-DANDROID_STL=c++_shared',
                    '-DQT_CMAKE_PREFIX_PATH=' + HIFI_ANDROID_PRECOMPILED + '/qt/lib/cmake',
                    '-DNATIVE_SCRIBE=' + HIFI_ANDROID_PRECOMPILED + '/scribe',
                    '-DHIFI_ANDROID_PRECOMPILED=' + HIFI_ANDROID_PRECOMPILED,
                    '-DRELEASE_NUMBER=' + RELEASE_NUMBER,
                    '-DRELEASE_TYPE=' + RELEASE_TYPE,
                    '-DBUILD_BRANCH=' + BUILD_BRANCH
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all {
                if (RELEASE_NUMBER != '0') {
                    outputFileName = "app_" + RELEASE_NUMBER + "_" + RELEASE_TYPE + ".apk"
                }
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path '../../CMakeLists.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: "${project.rootDir}/libraries/jar", include: 'QtAndroid-bundled.jar')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.vr:sdk-audio:1.80.0'
    compile 'com.google.vr:sdk-base:1.80.0'
}
